options {
  /* default values here:
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
  */
  
  // we define our own token manager
  USER_TOKEN_MANAGER = true;
  STATIC = false;
}

PARSER_BEGIN(WyvernParser)

package wyvern.tools.parsing.coreparser;

import wyvern.tools.typedAST.abs.*;
import wyvern.tools.typedAST.interfaces.*;
import wyvern.tools.typedAST.core.*;
import wyvern.tools.typedAST.core.values.*;
import wyvern.tools.typedAST.core.binding.*;
import wyvern.tools.typedAST.core.expressions.*;
import wyvern.tools.typedAST.core.declarations.*;
import wyvern.tools.errors.*;

import java.net.URI;
import java.util.*;

public class WyvernParser<AST,Type> {
    private int number = 0;
    
    public FileLocation loc(Token t) {
    	return new FileLocation(getTM().getFilename(), t.beginLine, t.beginColumn);
    }
    
    public WyvernTokenManager getTM() {
    	return (WyvernTokenManager) token_source;
    }
    
    public String freshName() {
    	return "ignoreme" + number++;
    }
    
    private ASTBuilder<AST,Type> build;
    public void setBuilder(ASTBuilder<AST,Type> builder) { build = builder; }

    private enum IndentedExpKind {
        NEW_KIND, DSL_KIND,
    }

	private class ExpFlags {
		public boolean isEmpty() { return exp == null; }
		public void setExp(AST a, IndentedExpKind kind) throws ParseException {
		  	// error to have two news in one expression
		  	if (!isEmpty())
		  		throw new ParseException("May not have two \"new\" or \"DSL literal\" expressions within the same line"); 
			exp = a;
			this.kind = kind;
		} 
		public AST getExp() { return exp; }
        public IndentedExpKind getExpKind() { return kind; }
		
	    private AST exp;
	    private IndentedExpKind kind;
	}    
}

PARSER_END(WyvernParser)

SPECIAL_TOKEN : /* Comments and whitespace */
{
  <SINGLE_LINE_COMMENT>
| <MULTI_LINE_COMMENT>
| <WHITESPACE>
}

TOKEN : /* Keywords */
{
  < VAL: "val" >
| < VAR: "var" >
| < DEF: "def" >
| < TYPE: "type" >
| < CLASS: "class" >
| < DELEGATE: "delegate" >
| < TO: "to" >
| < REQUIRE: "require" >
| < METADATA: "metadata" >
| < NEW: "new" >
| < MODULE: "module" >
| < COMPRISES: "comprises" >
| < EXTENDS: "extends" >
| < TAGGED: "tagged" >
| < MATCH: "match" >
| < IMPORT: "import" >
//| < : "" >
// more here
| < RESOURCE : "resource" >
| < INSTANTIATE : "instantiate" >
| < AS : "as" >
}

TOKEN : /* Logical Formatting */
{
  <NEWLINE>
| <INDENT>
| <DEDENT>
| <DSLLINE>
}

TOKEN : /* Identifiers */
{
  <IDENTIFIER>
}

TOKEN : /* Symbols */
{
  <LPAREN>
| <RPAREN>
| <TILDE>
| <PLUS>
| <DASH>
| <MULT>
| <DIVIDE>
| <MOD>
| <EQUALS>
| <EQUALSEQUALS>
| <COMMA>
| <ARROW>
| <TARROW>
| <DOT>
| <COLON>
| <POUND>
| <QUESTION>
| <BAR>
| <AND>
| <GT>
| <LT>
| <LBRACK>
| <RBRACK>
| <LBRACE>
| <RBRACE>
| <BOOLEANAND>
| <BOOLEANOR>
//| <>
}

TOKEN : /* Literals */
{
  < STRING_LITERAL >
| < DECIMAL_LITERAL >
| < BOOLEAN_LITERAL >
| < RATIONAL_LITERAL >
| < DSL_LITERAL >
}

/** Root production. */
AST CompilationUnit() :
{ AST exp; Token id; AST moduleArgs=null; Type t; boolean isResource = false; }
{
  LOOKAHEAD(2)
  <MODULE> <DEF> id = <IDENTIFIER> <LPAREN> [ moduleArgs = ModuleArgs() ] <RPAREN> OptType() <NEWLINE> exp = ModuleBody() {
	if (moduleArgs == null) {
	   return build.moduleDecl(id.image, exp, loc(id), true);
	} else {
	   return build.moduleDecl(id.image, build.sequence(moduleArgs,exp,true), loc(id), true);
	}
  }
|
  <MODULE> id = <IDENTIFIER> OptType() <NEWLINE> exp = ModuleBody() {
	return build.moduleDecl(id.image, exp, loc(id), isResource);
  }
|
  exp = TopLevelCode()  { return exp; }
}

AST ModuleArgs() :
{ AST arg; AST args=null; }
{
  arg = ModuleArg() {
      args = build.sequence(args,arg,true);
  }
  ( <COMMA> arg = ModuleArg() {
      args = build.sequence(args,arg,true);
    }
  )*
  { return args; }
}

AST ModuleArg() :
{ Token name; URI uri; }
{
  name = <IDENTIFIER> <COLON> uri = Uri() {
      return build.importDecl(uri, loc(name), name, true, false);
  }
}

Type OptType() :
{ Type t; }
{
  <COLON> t = Type()  { return t; }
|
  /* nothing */ { return null; }
}

AST ModuleBody() :
{ AST ast; AST decl; }
{
  ast = DeclSequence(true)  { return ast; }
|
  decl = ImportDecl() ast = ModuleBody() { return build.sequence(decl,ast,true); }
}

AST TopLevelCode() :
{ AST ast; AST decl; }
{
  ast = DeclSequence(false)  { return ast; }
|
  decl = RequireDecl() ast = TopLevelCode() { return build.sequence(decl,ast,false); }
|
  decl = ImportDecl() ast = TopLevelCode() { return build.sequence(decl,ast,false); }
}

AST RequireDecl() :
{ URI uri; Token t; Token name = null;}
{
  t=<REQUIRE> uri = Uri() [<AS> name = <IDENTIFIER>] <NEWLINE>
    { return build.importDecl(uri, loc(t), name, true, false); }
}


AST ImportDecl() :
{ URI uri; Token t; Token name = null; Token meta = null; }
{
  t=<IMPORT> [meta=<METADATA>] uri = Uri() [<AS> name=<IDENTIFIER>] <NEWLINE>
    { return build.importDecl(uri, loc(t), name, false, meta != null); }
}

URI Uri() :
{ Token t; String scheme = null; String s1; String s2; URI uri; }
{
	[ LOOKAHEAD(2) t = <IDENTIFIER> <COLON> { scheme = t.image; } ]
	s1 = Segment() ( <DIVIDE> s2 = Segment() { s1 = s1 + '/' + s2; } )* {
		if (scheme == null)
			scheme = "wyv";
   		try {
			return new URI(scheme, s1, null);
   		} catch (Exception e) { throw new RuntimeException(e); }
	}
}

String Segment() :
{ String s; Token t1; }
{
	t1 = <IDENTIFIER> { s = t1.image; }	
	( <DOT> t1 = <IDENTIFIER> { s = s + '.' + t1.image; } )* { return s; }
}

AST DeclSequence(boolean inModule) :
{ AST decl1; AST decl2; }
{
  decl1 = Declaration(inModule)
  ( decl2 = Declaration(inModule) { decl1 = build.sequence(decl1, decl2, inModule); } )*
    { return decl1; }
}

AST Declaration(boolean inModule) :
{ AST exp; }
{
  exp = DefDeclaration()  { return exp; }
|
  exp = ValDeclaration()  { return exp; }
|
  exp = VarDeclaration()  { return exp; }
|
   LOOKAHEAD(4) exp =   TypeDeclaration()  { return exp; }
|
   LOOKAHEAD(4) exp = TypeAbbrevDeclaration() { return exp; }
|
  exp = DelegateDeclaration()  { return exp; }
|
  exp = ExpressionLine(inModule) { return exp; }
|
  exp = Instantiation() {return exp; }
}

AST DefDeclaration() :
{ Type resultType; Token name; AST body; List args; List<String> gentypes = null; }
{
  <DEF> (name=<IDENTIFIER> | name = OverrideOperator()) [gentypes = Generics()] args = Formals() <COLON> resultType = Type() body=DefBody() {
	return build.defDecl(name.image, resultType, gentypes, args, body, false, loc(name));
  }
}

AST ValDeclaration() :
{ Type type = null; Token name; AST body; List args; }
{
  <VAL> name=<IDENTIFIER> [ <COLON> type = Type() ] <EQUALS> body = ExpressionLine(false) {
	return build.valDecl(name.image, type, body, loc(name));
  }
}

AST TypeAbbrevDeclaration() :
{ Token t = null; Type reference = null; Token alias = null; }
{
   t = <TYPE> alias = <IDENTIFIER>
    ( <EQUALS> reference = Type())? <NEWLINE> {
     return build.typeAbbrevDecl(alias.image, reference, loc(t));
   }
}

AST VarDeclaration() :
{ Type type; Token name; AST body; List args; }
{
  <VAR> name=<IDENTIFIER> <COLON> type = Type() <EQUALS> body = ExpressionLine(false) {
	return build.varDecl(name.image, type, body, loc(name));
  }
}

AST DelegateDeclaration() :
{ Type type; Token t; AST exp; }
{
  t=<DELEGATE> type=Type() <TO> exp=ExpressionLine(false) {
	return build.delegateDecl(type, exp, loc(t));
  }
}

AST TypeDeclaration() :
{ Token name; AST body = null; AST metadata = null; Type extendedType=null; List<Type> comprisesList=null; boolean isTagged = false;
  Token selfName = null;
  Object tagInfo = null; boolean isResource = false;
}
{
  [<TAGGED> { isTagged = true; } ]
  [<RESOURCE> { isResource = true; } ]
  <TYPE> name=<IDENTIFIER>
  [ <EXTENDS> extendedType=Type() ]
  [ <COMPRISES> comprisesList=TypeList() ]
  [ <LPAREN> selfName=<IDENTIFIER> <RPAREN> <ARROW> ]
  <NEWLINE> <INDENT> body=DeclTypes()
                    [ <METADATA> metadata = ExpressionLine(false) ]
             <DEDENT> {
    if (isTagged || (extendedType != null) || (comprisesList != null)) {
      tagInfo = build.tagInfo(extendedType, comprisesList);
    }
    String self = (selfName == null) ? null : selfName.image;
	return build.typeDecl(name.image, body, tagInfo, metadata, loc(name), isResource, self);
  }
}

AST DeclTypes() :
{ AST decl1; AST decl2; AST body; }
{
  decl1 = DeclType()
  ( decl2 = DeclType() { decl1 = build.sequence(decl1, decl2, true); } )*
    { return decl1; }
}

AST DeclType() :
{ AST exp; }
{
  exp = DefDeclType()  { return exp; }
|
  exp = ValDeclType()  { return exp; }
|
  exp = VarDeclType()  { return exp; }
|
  LOOKAHEAD(4) exp = TypeDeclaration()  { return exp; }
|
  exp = TypeAbbrevDeclaration() { return exp; }
}

AST DefDeclType() :
{ Type resultType; Token name; List args; }
{
  <DEF> (name = <IDENTIFIER> | name = OverrideOperator()) args = Formals() <COLON> resultType = Type() <NEWLINE> {
	return build.defDeclType(name.image, resultType, args, loc(name));
  }
}

AST ValDeclType() :
{ Type type; Token name; List args; }
{
  <VAL> name=<IDENTIFIER> <COLON> type = Type() <NEWLINE> {
	return build.valDeclType(name.image, type, loc(name));
  }
}

AST VarDeclType() :
{ Type type; Token name; List args; }
{
  <VAR> name=<IDENTIFIER> <COLON> type = Type() <NEWLINE> {
  return build.varDeclType(name.image, type, loc(name));
  }
}

List Formals_Lambda() :
{ List args = new LinkedList(); Object singleParameter;}
{     singleParameter = Formal_Lambda() { 
            args.add(singleParameter);
            return args;
        }
   | <LPAREN> [ FormalsList_Lambda(args) ] <RPAREN> {
			return args;
		}
}

void FormalsList_Lambda(List args):
{ Object formal;}
{
  formal = Formal_Lambda() {
      args.add(formal);
  }
  ( <COMMA> formal = Formal_Lambda() {
      args.add(formal);
	}
  )*
}

Object Formal_Lambda() :
{ Token id; Type type = null; }
{
  id = <IDENTIFIER> [<COLON> type = Type()] {
      return build.formalArg(id.image, type);
  }
}

List Formals() :
{ List args = new LinkedList(); }
{
  <LPAREN> [ FormalsList(args) ] <RPAREN> {
			return args;
		}
}

void FormalsList(List args) :
{ Object formal; }
{
  formal = Formal() {
      args.add(formal);
  }
  ( <COMMA> formal = Formal() {
      args.add(formal);
	}
  )*
}

Object Formal() :
{ Token id; Type type; }
{
  id = <IDENTIFIER> <COLON> type = Type() {
      return build.formalArg(id.image, type);
  }
}

AST DefBody() :
{ AST exp; }
{
  <NEWLINE> <INDENT> exp = DeclSequence(false) <DEDENT> { return exp; }
|
  <EQUALS> exp = ExpressionLine(false) { return exp; }
}

Token DSLLines() :
{ Token t, t2; String content = ""; }
{
    t=<DSLLINE> {
      content = t.image;
  }
  ( t2 = <DSLLINE> {
      content = content + t2.image;
    }
  )*
  { 
    t.image = content;
    return t;
  }
}

AST ExpressionLine(boolean makeValDecl) :
{ AST exp; AST decls = null; Token t; Token t2 = null; Object caseArm; List cases; ExpFlags flags = new ExpFlags(); }
{
  exp = Expression(flags) t=<NEWLINE>
  [
    t2=<INDENT> decls = DeclSequence(true) <DEDENT>
  |
    t2=DSLLines() 
  ]
  {
    if (!flags.isEmpty()) {
        if (flags.getExpKind() == IndentedExpKind.NEW_KIND) {
            build.setNewBody(flags.getExp(), decls);
    	} else if (t2 == null) {
            ToolError.reportError(ErrorMessage.EXPECTED_DSL_BLOCK, (HasLocation)flags.getExp());
        } else {
    	    build.setDSLBody(flags.getExp(), t2.image);
    	}
    } else if (t2 != null) {
        ToolError.reportError(ErrorMessage.ILLEGAL_INDENTED_BLOCK, loc(t2));
    }
    
    // create a decl if requested (e.g. we are within a module)
    if (!makeValDecl)
    	return exp;
  	else
  		return build.valDecl(freshName(), null, exp, loc(t));
  }
|
  t=<MATCH> exp=Expression(flags) <COLON> <NEWLINE> { cases = new LinkedList(); }
  <INDENT>
  ( caseArm=CaseArm() { cases.add(caseArm); } )*
  <DEDENT> {
    return build.match(exp, cases, loc(t));
  }
}

Object CaseArm() :
{ Token id; Token t; AST exp; Type type; }
{
  id=<IDENTIFIER> <COLON> type=Type() t=<ARROW> exp=ExpressionLine(false) {
    return build.caseArm(id.image,type,exp,loc(t));
  }
}

AST Expression(ExpFlags flags) :
{ AST exp; AST exp2 = null; Object formal; Token t = null; }
{
  LOOKAHEAD(4)
    exp = LambdaFn(flags) { return exp; }
|  
    exp = AssignmentExpression(flags) { return exp; }
}

AST AssignmentExpression(ExpFlags flags) :
{ AST exp; AST exp2 = null; Object formal; Token t = null; }
{
  exp = BooleanRelationalExpression(flags) [ t=<EQUALS> exp2 = Expression(flags) ] {
    if (exp2 != null) {
        return build.assignment(exp, exp2, loc(t));
    }
    return exp;
  }
}

AST LambdaFn(ExpFlags flags) :
{ List formals; AST body; Token t; }
{
    formals = Formals_Lambda() t=<ARROW> body = Expression(flags) {
        return build.fn(formals, body, loc(t));
    }
}



List<Type> TypeList() :
{ List<Type> types = new LinkedList<Type>(); Type type; }
{
  type=Type() { types.add(type); }
  ( <COMMA> type=Type() { types.add(type); } )*
  { return types; }
|
  { return types; }
}

Type Type() :
{ Token id = null; Token id2 = null; AST exp = null; Type arrowResult = null; }
{
  id = <IDENTIFIER> (<DOT> id2 = <IDENTIFIER> {
	if (exp == null) {
	  	exp = build.var(id.image, loc(id));
	} else {
		exp = build.invocation(exp, id.image, null, loc(id));
	}
  	id = id2;
 })* (<TARROW> arrowResult = Type() )? {

    Type t;
    if (exp == null) {
        t = build.nominalType(id.image, loc(id));
    } else {
        t = build.qualifiedType(exp, id2.image);
    }

    if (arrowResult == null) {
        return t;
    } else {
        return build.arrowType(t, arrowResult);
    }
 }
}

AST BooleanRelationalExpression(ExpFlags flags) :
{ AST exp; AST exp2 = null; Token t; }
{
    exp = RelationalExpression(flags)
    [ ( t=<BOOLEANAND> | t=<BOOLEANOR> ) exp2=BooleanRelationalExpression(flags) {
      exp = build.invocation(exp, t.image, exp2, loc(t));
    } ]
    { return exp; }
}

AST RelationalExpression(ExpFlags flags) :
{ AST exp; AST exp2 = null; Token t; }
{
  exp = AdditiveExpression(flags)
  [ ( t=<GT> | t=<LT> | t=<EQUALSEQUALS> ) exp2=AdditiveExpression(flags) {
    exp = build.invocation(exp, t.image, exp2, loc(t)); 
  } ]
  { return exp; }
}

AST AdditiveExpression(ExpFlags flags) :
{ AST exp; AST exp2 = null; Token t; }
{
  exp = MultiplicativeExpression(flags)
  ( ( t=<PLUS> | t=<DASH> ) exp2=MultiplicativeExpression(flags) {
    exp = build.invocation(exp, t.image, exp2, loc(t)); 
  } )*
  { return exp; }
}

AST MultiplicativeExpression(ExpFlags flags) :
{ AST exp; AST exp2 = null; Token t; }
{
  exp = UnaryExpression(flags)
  ( ( t=<MULT> | t=<DIVIDE> | t=<MOD> ) exp2=UnaryExpression(flags) {
  	exp = build.invocation(exp, t.image, exp2, loc(t));
  } )*
  { return exp; }
}

AST UnaryExpression(ExpFlags flags) :
{ AST exp; Token t; }
{
  <PLUS> exp = UnaryExpression(flags) { return exp; }
|
  t=<DASH> exp = UnaryExpression(flags) {
    return build.invocation(exp, "negate", build.unitValue(loc(t)), loc(t));
  }
|
  exp = PrimaryExpression(flags) { return exp; }
}

AST PrimaryExpression(ExpFlags flags) :
{ AST exp; Token t; Token selfName = null; }
{
  exp = PrimaryPrefix(flags) ( exp = PrimarySuffix(exp,flags) )*  { return exp; }
|
  t = <NEW> [ <LPAREN> selfName=<IDENTIFIER> <RPAREN> <ARROW>] {
    String self = (selfName == null) ? null : selfName.image;
    exp = build.newObj(loc(t), self);
    
    // store a reference to the new expression so we can fill in the fields later!
    flags.setExp(exp, IndentedExpKind.NEW_KIND);
    return exp;
  }
}

AST PrimaryPrefix(ExpFlags flags) :
{ AST exp; Token t; }
{
  exp = Literal() { return exp; }
|
  t = <IDENTIFIER> { return build.var(t.image, loc(t)); }
|
  <LPAREN> exp = Expression(flags) <RPAREN> { return exp; }
|
  t=<TILDE> {
    exp = build.dsl(loc(t));
    flags.setExp(exp, IndentedExpKind.DSL_KIND);
    return exp;
  }
}

AST Literal() :
{ AST exp; Token lit; }
{
  lit = <STRING_LITERAL>  { return build.stringLit(lit.image, loc(lit)); }
|
  lit = <DECIMAL_LITERAL> { return build.integerLit(Integer.parseInt(lit.image), loc(lit)); }
|
  lit = <BOOLEAN_LITERAL> { return build.booleanLit(Boolean.parseBoolean(lit.image), loc(lit)); }
|
  lit = <DSL_LITERAL> {
    exp = build.dsl(loc(lit));
    build.setDSLBody(exp, lit.image);
    return exp;
  }
|
  <RATIONAL_LITERAL>
}

AST PrimarySuffix(AST lhs, ExpFlags flags) :
{ AST rhs; Token t; List<String> gentypes = null;}
{
  <DOT> t = <IDENTIFIER> { return build.invocation(lhs, t.image, null, loc(t)); }
|
  [gentypes = Generics()] rhs = Arguments(flags) {
    return build.application(lhs, rhs, null, gentypes);
  }
}

AST Arguments(ExpFlags flags) :
{ AST exp = null; Token t; }
{
  t = <LPAREN> [ exp = ArgumentList(flags, loc(t)) ] <RPAREN> {
    if (exp == null) {
        return build.unitValue(loc(t));
    }
    else {
        return exp;
    }
  }
}

AST ArgumentList(ExpFlags flags, FileLocation loc) :
{ AST exp; AST exp2; LinkedList<AST> expList = null; Token t; }
{
  exp = Expression(flags)
  ( t=<COMMA> exp2 = Expression(flags) {
  		if (expList == null) {
  			expList = new LinkedList<AST>();
  			expList.add(exp);
  		}
  		expList.addLast(exp2);
	}
  )* {
  		if (expList != null) {
  			return build.tuple(expList, loc); 
  		}
  		return exp;
  	}
 }
  	
  	
AST Instantiation() :
{ URI uri; Token name; AST arg;}
{
<INSTANTIATE> uri = Uri() arg = Arguments(null) <AS> name=<IDENTIFIER> <NEWLINE> 
{return build.instantiation(uri, arg, name, loc(name));}
}

Token OverrideOperator() :
{Token operator;}
{
      operator = <PLUS> {return operator;}
	| operator = <DASH> {return operator;}
	| operator = <MULT> {return operator;}
    | operator = <DIVIDE> {return operator;}
    | operator = <MOD> {return operator;}

}

List Generics() :
{ List gens = null; }
{
  <LBRACK> gens = GenericList() <RBRACK> {
    return gens;
  }
}

List GenericList() :
{ LinkedList<String> genList = new LinkedList<String>(); Token t1, t2; }
{

  t1 = <IDENTIFIER>
  ( <COMMA> t2 = <IDENTIFIER> {
    genList.addLast(t2.image);
  }
  )* {
    genList.addFirst(t1.image);
    return genList;
  }
}

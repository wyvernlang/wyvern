module parser

import wyvern.util.matching.regex
import tsls.lexer
import wyvern.ast
import wyvern.Int
import wyvern.option
import debug

// local bindings to make things easier
val tokens = lexer.tokens
type AST = ast.AST

// A parse function for reverse Polish notation
def parse(input:String):option.Option
	// Create a scanner configuration, setting up the whitespace operator
	val config = lexer.makeScannerConfig(regex("[ \n\t]+"))

	// Add regular expressions and corresponding tokens
	config.addTokenKind(regex("\\d+")      , tokens.NUMBER)
	config.addTokenKind(regex("[\\+\\-\\*/]"), tokens.OPERATOR)
	config.addTokenKind(regex("\\z")       , tokens.EOF)

	// create a scanner and scan the input
	val scanner = lexer.makeScanner(config, input)
	//debug.print(input)
	doParse(scanner)

// The starting point for recursive-descent parsing	
def doParse(scanner:lexer.Scanner):option.Option
	val t = scanner.nextToken()
	(t.kind() == tokens.NUMBER).ifTrue(
	  () => parseWithOneExpr(scanner, ast.int(Int.from(t.print()))),
	  () => option.None()
	)

// We've got exactly one expression on the stack
def parseWithOneExpr(scanner:lexer.Scanner, exp:AST):option.Option
	val t = scanner.nextToken()
	(t.kind() == tokens.EOF).ifTrue(
		() => option.Some(exp),
		() => (t.kind() == tokens.NUMBER).ifTrue(
			() => parseWithTwoExprs(scanner, exp, ast.int(Int.from(t.print())), false),
			() => option.None()
		)
	)

// We've got two (ore more, if moreOnStack is true) expressions on the stack
def parseWithTwoExprs(scanner:lexer.Scanner, exp1:AST, exp2:AST,
                      moreOnStack:Boolean):option.Option
	def compose(exp3:AST):option.Option
		val expOption : option.Option = parseWithTwoExprs(scanner, exp2, exp3, true)
		expOption.flatMap((expResult) => parseWithTwoExprs(scanner, exp1,expResult, moreOnStack))
	val t = scanner.nextToken()
	(t.kind() == tokens.OPERATOR).ifTrue(
		() => moreOnStack.ifTrue(
			() => option.Some(ast.call(exp1,t.print(),exp2)),
		    () => parseWithOneExpr(scanner, ast.call(exp1,t.print(),exp2))
		),
		() => (t.kind() == tokens.NUMBER).ifTrue(
			() => compose(ast.int(Int.from(t.print()))),
			() => option.None()
		)
	)

/** MODULE: Simple Wyvern class example from http://www.cs.cmu.edu/~aldrich/securemobileweb/spec-rationale.html **/


/** Public Interface **/
type IntCell
	var contents : Int

type Stack
	prop top : Int?
	meth push(element : Int)
	meth pop() : Int?

type StackFactory
	meth Stack() : Stack
	meth StackWithFirst(firstElement : Int) : Stack


/** Private Implementation **/
class Link
	val data : Int
	val next : Link?

	class meth Link(d:Int, n:Link?) : Link
		new
			data=d
			next=n


class StackImpl
	implements Stack
	class implements StackFactory

	var list : Link?

	meth top() : Int? = this.list.data

	meth push(element : Int) : Unit
		this.list = Link.Link(element, (this.list))

	meth pop() : Int?
		val result : Int = this.list.data
		this.list = this.list.next
		result

	class meth Stack() : Stack = new
	
	class meth StackWithFirst(firstElement : Int) : Stack
		new
			list = Link(firstElement, null)


meth stackClient()
	val s : Stack = StackImpl.Stack()
	s.push(5)
	printInteger(s.top)
	val addOne:Int->Int = fn x:Int => x+1
	printInteger(addOne(s.pop()))
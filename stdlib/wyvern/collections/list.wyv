module list

import wyvern.internal.ast
import wyvern.internal.list
import wyvern.option
import wyvern.util.matching.regex
import metadata wyvern.IfTSL

type List = list.List
  metadata new
    def parseTSL(input : String, ctx : system.Context) : option.Option[ast.AST]
      val exprList : list.List[ast.AST] = ast.parseExpressionList(input, ctx)
      val n = exprList.length()
      val listIdent = ast.genIdent()
      def build(i : Int, listAST : ast.AST) : ast.AST
        if (i > 0)
            val appendArgs : list.List[ast.AST] = list.make[ast.AST]()
            appendArgs.append(exprList.get(i - 1).getOrElse(() => ast.string("")))
            val appendAST : ast.AST = ast.call(ast.variable(listIdent), "append", appendArgs)
            build(i - 1, ast.let(ast.genIdent(), ast.types.unit(), appendAST, listAST))
          else
            listAST
      val appendsAST = build(n, ast.variable(listIdent))
      val typeParams = list.make[ast.Type]()
      typeParams.append(ast.types.dyn())
      val identType = ast.types.refinement(typeParams, ast.types.nominal("list", "List"))
      option.Some[ast.AST](ast.let(listIdent, identType, ast.call(ast.variable("list"), "makeD", list.make[ast.AST]()), appendsAST))

def make[T]():List[T] = list.make[T]()
def makeD():Dyn = list.make[Dyn]()


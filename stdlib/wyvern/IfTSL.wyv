module IfTSL

import wyvern.internal.ast
import wyvern.option
import wyvern.util.matching.regex
import wyvern.internal.list              // We use wyvern.internal.list because wyvern.collections.list depends on IfTSL

def thenMatch(input : String) : regex.Match
  val thenRegex = regex("^then\n")
  val mOpt = thenRegex.findPrefixMatchOf(input)
  mOpt.getOrElse(() => regex.makeMatch("", input))

def elseMatch(input : String) : regex.Match
  val thenRegex = regex("^else\n")
  val mOpt = thenRegex.findPrefixMatchOf(input)
  mOpt.getOrElse(() => regex.makeMatch("", input))

def indentedBlockMatch(input : String) : regex.Match
  val blockRegex = regex("(\\s[^\n]*\n)+")
  val blockMatchOpt = blockRegex.findPrefixMatchOf(input)

  val emptymatch = regex.makeMatch("", input)
  val m : regex.Match = blockMatchOpt.getOrElse(() => emptymatch)
  m

def toUnitOrAST(input : String, ctx : system.Context) : ast.AST
  val stripped = ast.stripLeadingWhitespace(input)
  (input == "").ifTrue(() => ast.int(0), () => ast.parseExpression(stripped, ctx))

type AST = ast.AST

resource type IfExpr
  def evalTrue() : Dyn
  def evalFalse() : Dyn
  metadata new
    def parseTSL(input : String, ctx : system.Context) : option.Option
      val trimmedInput = ast.stripLeadingWhitespace(input)
      val tm = thenMatch(trimmedInput)
      val trueMatch = indentedBlockMatch(tm.after())
      val em = elseMatch(trueMatch.after())
      val trueAST : AST = toUnitOrAST(trueMatch.matched(), ctx)
      val falseAST : AST = toUnitOrAST(em.after(), ctx)
      val trueDefn = ast.defn("evalTrue", ast.types.dyn(), trueAST)
      val falseDefn = ast.defn("evalFalse", ast.types.dyn(), falseAST)
      val declList : list.List = list.make()
      declList.append(trueDefn)
      declList.append(falseDefn)
      option.Some(ast.object(declList))

resource type IfExprR
  def evalTrue() : Dyn
  def evalFalse() : Dyn
  metadata new
    def parseTSL(input : String, ctx : system.Context) : option.Option
      val trimmedInput = ast.stripLeadingWhitespace(input)
      val tm = thenMatch(trimmedInput)
      val trueMatch = indentedBlockMatch(tm.after())
      val em = elseMatch(trueMatch.after())
      val trueAST : AST = toUnitOrAST(trueMatch.matched(), ctx)
      val falseAST : AST = toUnitOrAST(em.after(), ctx)
      val trueDefn = ast.defn("evalTrue", ast.types.dyn(), trueAST)
      val falseDefn = ast.defn("evalFalse", ast.types.dyn(), falseAST)
      val declList : list.List = list.make()
      declList.append(trueDefn)
      declList.append(falseDefn)
      option.Some(ast.object(declList))

def doif(condition : Boolean, body : IfExpr) : Dyn
  condition.ifTrue(() => body.evalTrue(), () => body.evalFalse())

def doifR(condition : Boolean, body : IfExprR) : Dyn
  val bodyR : Dyn = body
  doif(condition, bodyR)